plugins { id 'java' }

def HOME_DIR = System.getProperty("user.home")
def GAME_DIR = "${HOME_DIR}/Library/Application Support/Steam/steamapps/common/ProjectZomboid/Project Zomboid.app"
def GAME_JAVA_DIR = "${GAME_DIR}/Contents/Java"

dependencies {
    compileOnly files("${GAME_JAVA_DIR}/ZombieBuddy.jar")
    compileOnly files("${HOME_DIR}/projects/zomboid/42.12/java")
    
    // Test dependencies - need game classes for test compilation and runtime
    testImplementation files("${GAME_JAVA_DIR}/ZombieBuddy.jar")
    testImplementation files("${HOME_DIR}/projects/zomboid/42.12/java")
    
    // JUnit dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.10.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

// place all .java files in the 'src' directory
sourceSets {
    main {
        java {
            srcDirs = ['src']
            include '**/*.java'
            exclude '**/test/**'
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

// disable annotation processing, JNI headers, and incremental compilation
tasks.withType(JavaCompile).configureEach {
    options.annotationProcessorPath = files()
    options.generatedSourceOutputDirectory.set(provider { null })
    options.headerOutputDirectory.set(provider { null })
    options.incremental = false
}

tasks.register("cleanup") {
    doLast {
        delete "${buildDir}/tmp"
        delete "${buildDir}/classes"
    }
}

tasks.named("build") {
    finalizedBy("cleanup")
}

// Configure test task
test {
    useJUnitPlatform()
}

